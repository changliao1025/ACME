;================================================;
;  test11-lat-lon.ncl
;  produces the lat-lon plots for test 11
;================================================;
load "$NCARG_ROOT/lib/ncarg/nclscripts/csm/gsn_code.ncl"   
load "$NCARG_ROOT/lib/ncarg/nclscripts/csm/gsn_csm.ncl"
load "$NCARG_ROOT/lib/ncarg/nclscripts/csm/contributed.ncl"   
load "$NCARG_ROOT/lib/ncarg/nclscripts/csm/shea_util.ncl"   
; ================================================;

begin

  ; open file and read in data(time,lev,lat,lon) from 0 to n-1
  f    = addfile("./movies/dcmip2016_test21.nc","r")
	lat  = f->lat
	lon  = f->lon
	lev  = f->lev
  time = f->time
	nlat = getfilevardimsizes(f, "lat" )
	nlon = getfilevardimsizes(f, "lon" )
	nlev = getfilevardimsizes(f, "lev" )
  nt   = dimsizes(time)

  t_index = nt-1
  if(isvar("t")) then
    t_index = t         ; use t from command line if possible
  end if
  print ("t_index="+t_index)

  ; load data slice from file

  modheight = nlev-4

	u = f->u (t_index,modheight,:,:)
	v = f->v (t_index,modheight,:,:)
  ps= f->ps(t_index,:,:)

  u&lat@units = "degrees_north"
  u&lon@units = "degrees_east"

  v&lat@units = "degrees_north"
  v&lon@units = "degrees_east"

  spd = sqrt(u*u+v*v)
  copy_VarMeta(u,spd)
  
  ; find lat,lon of minimum pressure
  dims    = dimsizes(ps)
  ps_1d   = ndtooned(ps)      ; convert 2D array to 1D for use in maxind
  inds    = ind_resolve(minind (ps_1d), dims)    ; convert 1D array back to 2D
  ilat    = inds(0,0)         ; select the latitude index where the X array is at its' maximum
  ilon    = inds(0,1)         ; select the longitude index where the X array is at its' maximum
  lat_min = ps&lat(ilat)      ; insert the latitude index into the lat coordinate variable
  lon_min = ps&lon(ilon)      ; insert the longitude index into the lon coordinate variable
  print("Minum pressure located at "+lat_min+", "+lon_min)

  res                           =  True              ; plot mods desired
  res@gsnLeftString             = "Wind"

  res@vcRefMagnitudeF           = 10.0             ; define vector ref mag
  res@vcRefLengthF              = 0.045            ; define length of vec ref
  res@vcGlyphStyle              = "CurlyVector"    ; turn on curly vectors
  res@vcMinDistanceF            = 0.017            ; thin vectors
  res@vcRefAnnoOrthogonalPosF   = .1               ; move ref vector down
  res@gsnScalarContour          = True
  res@cnFillOn                  = True
  res@cnFillPalette             = "gui_default"     ; set color map
  res@cnLinesOn                 = False             ; turn off contour lines

  ; zoom in on a region
  w = 30.0 ;dg
  ;res@trXMinF =  lon_min - w
  ;res@trXMaxF =  lon_min + w
  ;res@trYMinF =  lat_min - w
  ;res@trYMaxF =  lat_min + w

  res@mpLimitMode       = "Corners"         ; choose range of map
  res@mpLeftCornerLatF  = lat_min - w
  res@mpLeftCornerLonF  = lon_min - w
  res@mpRightCornerLatF = lat_min + w
  res@mpRightCornerLonF = lon_min + w

  wks_type                  = "pdf"
  wks_type@wkPaperHeightF   = 8
  wks_type@wkPaperWidthF    = 11
	wks  = gsn_open_wks(wks_type,"vecplot_t"+t_index)

  plot = gsn_csm_vector_scalar_map(wks,u,v,spd,res)

end


  
